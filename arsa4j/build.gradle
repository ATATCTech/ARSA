plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.atatctech'
version '1.1.8'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('charset', 'UTF-8')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'arsa'
            description = 'A multilingual RSA library with segmented encryption and decryption and a unified format.'
            from components.java

            pom {
                name = 'ARSA'
                description = 'A multilingual RSA library with segmented encryption and decryption and a unified format.'
                url = 'https://github.com/ATATC/ARSA'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ATATC'
                        name = 'ATATC'
                        email = 'futerry@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/ATATC/ARSA.git'
                    developerConnection = 'scm:git:https://github.com/ATATC/ARSA.git'
                    url = 'https://github.com/ATATC/ARSA'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}

test {
    useJUnitPlatform()
}